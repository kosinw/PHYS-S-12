---
layout: post
title:  "Electronic Output Devices"
permalink: /09/
visible: true
---

## 2019, July 25 (Thursday Section)

### MOSFETs
MOSFETs or metal-oxide-semiconductor field-effect transistors are a type of transistor (specifically an active element controlled through electronics) which allows current to flow from source to ground when an electrical signal is applied to gate. The amount of current allowed through the FET can be altered either using an analog or [PWM]({{ "/02#pwm" | prepend: site.baseurl }}) signal. MOSFETs are very useful for controlling an output device that requires an external (relative to a microcontroller) electrical signal yet still be able to control a state using a micocontroller.

### BJTs
BJTs or bipolar junction transistors are used for lower current applications than MOSFETs and have different internal workings. Functionally, both transistors are used to amplify signals and use three terminals.

### H-Bridges
H-Bridges are a method to control which direction the voltage differential flows across a device such as a DC motor. Internally, the h-bridge uses 4 transistors to control the flow of current through the device and is controlled through a microcontroller to affect the polarity of an output device.

### DC motors
DC motors are a type of active element that converts electrical energy into rotary mechanical energy. The polarity across the leads of the DC motor also control which direction the DC motor will actuate. DC motors work by using a combination of electromagnets (coiled wire that generates magnetic field depending on the direction of current) and magnets. Depending on which way the magnetic field is generated will cause torque to be generated.

### Stepper motors
Stepper motors have moderate power and torque and can be used to turn very precise positions by alternating between stable states to move a set distance.

### Servomotors
Servomotors are motors that can move to an exact programmed position. These range from 0 to &#960; radians. These motors have embedded microcontrollers which allow more precise communication with other microcontrollers. Servomotors generally have low power and cannot be used for applications which need to move great amounts of mass.

### Solenoids
Solenoids, also known as electromagnets, generate an electromagnetic field in response to an electrical current flowing through a coil. This coil can be created through rather rudimentary means such as wrapping wire around a dowel. Even simplistic solenoids require more current than the Arduino can supply so they must be used in conjunction with some sort of transistor.

### Piezoelectric speakers
Piezoelectric buzzers or speakers work in the opposite direction as [piezoelectric sensors]({{"/07#piezoelectricity" | prepend: site.baseurl }}). When an oscillating electrical signal is applied to the piezoelectric material it flexes upwards or downwards and creates a vibration that propagates through air (how sound works). Usually, these buzzers are connected to some sort of audio amplifier to create an audible tone for humans. Thankfully, Arduino's `tone()` function handles the generation of an oscillation signal by using the Arduino's inbuilt crystal oscillator. Music such as the [Super Mario Bros. Themes](https://www.princetronics.com/supermariothemesong/) or the Harry Potter theme can be created:

```cpp
int led_pin = 13;
int buzzer_pin = 11;

struct MusicStruct {
  int A = 550;
  int As = 582;
  int B = 617;
  int C = 654;
  int Cs = 693;
  int D = 734;
  int Ds = 777;
  int E = 824;
  int F = 873;
  int Fs = 925;
  int G = 980;
  int Gs = 1003;
  int A2 = 1100;
  int A2s = 1165;
  int B2 = 1234;
  int C3 = 1308;
  int C3s = 1385;
  int D3 = 1555;
}Music;

struct LengthStruct {
  float half = 0.5;
  float one = 1.0;
  float one_half = 1.5;
  float two = 2.0;
  float two_half = 2.5;
}Length;

int tempo = 400;

void setup() {
  pinMode(led_pin, OUTPUT);
  pinMode(buzzer_pin, OUTPUT);
}

void setTone(int pin, int note, int duration) {
  tone(pin, note, duration);
  delay(duration);
  noTone(pin);
}

void loop() {
  setTone(buzzer_pin, Music.B, tempo * Length.one);
  setTone(buzzer_pin, Music.E, tempo * Length.one_half);
  setTone(buzzer_pin, Music.G, tempo * Length.half);
  setTone(buzzer_pin, Music.F, tempo * Length.one);
  setTone(buzzer_pin, Music.E, tempo * Length.two);
  setTone(buzzer_pin, Music.B2, tempo * Length.one);
  setTone(buzzer_pin, Music.A2, tempo * Length.two_half);
  setTone(buzzer_pin, Music.Fs, tempo * Length.two_half);
  
  setTone(buzzer_pin, Music.E, tempo * Length.one_half);
  setTone(buzzer_pin, Music.G, tempo * Length.half);
  setTone(buzzer_pin, Music.F, tempo * Length.one);
  setTone(buzzer_pin, Music.Ds, tempo * Length.two);
  setTone(buzzer_pin, Music.F, tempo * Length.one);
  setTone(buzzer_pin, Music.B, tempo * Length.two_half);
  delay(500000);
}
```

### LED arrays
Due to the limited amount of pins on development boards such as the Arduino, controlling a large amount of LEDs especially RGB LEDs (which singular ones require 3 differnt PWM pins) becomes impossible. Therefore strips of LEDs have been created which allow the use of many LEDs at one time using libraries such as [Adafruit NeoPixel](https://learn.adafruit.com/adafruit-neopixel-uberguide?embeds=allow). Each LED in the strip is embedded with a small circuit that allows for addressing and control for each individual LED through only 3 lines (V<sub>in</sub>, GND, and V<sub>0</sub>).

## 2019, July 29 (Monday Lab)

### Radio transceiver
Although debatebly not an output device, the NRF24LO1 module ([RF24](#9a) for short) acts as a radio transceiver. A transceiver is a portmanteau of "transmitter" and "receiver" and aptly acts as terminology to describe a device that can act as both a radio transmitter and receiver. The [RF24](#9a) is an ultra low power, bidirectional radio device that operates at an average frequency of 2.4GHz (hence the 24 in RF<i>24</i>). The [library](https://github.com/nRF24/RF24) I used to work with the RF24 modules can only send 8 bits of data at one time which can be solved by implementing some sort of [protocol](https://en.wikipedia.org/wiki/Communication_protocol) (about layer 4 on the OSI model).

<figure id="9a">
  <img src="./rf24.jpg">
  <p><a>Figure 9A</a>: An NRF24L01 module. Bidirectional radio frequency device that has a radio wave oscillating at 2.4GHz.</p>
</figure>

### Building the circuit
For the assignment due on Tuesday's section, I built out two circuits (one [transmitter](#9b), one [receiver](#9c)) which use the [RF24 module](#9a) for unidirectional radio communication. Every one loop cycle, the [transmitter](#9b) circuit uses an Arduino Uno development board and reads in analog input (A0) from a 1k potentiometer, then the Arduino Uno sends the analog input (0 to 1023) as a byte (0 to 255) across a radio frequency (which inevitably means lossy conversion as bytes are integer types). The transmitter communicates with the [RF24 module](#9a) over [serial packet interface (i.e., SPI)](https://en.wikipedia.org/wiki/Serial_Peripheral_Interface), which is a similar communication protocol to I<sup>2</sup>C. 

<figure id="9b">
  <img src="./tx.jpg">
  <p><a>Figure 9B</a>: The transmitter circuit. An Arduino Uno which connects to a 1k pot and an RF24 module over SPI communication.</p>
</figure>

The other circuit I built was a [receiver](#9c) circuit. Every loop cycle, the Arduino Micro uses its [RF24 module](#9a) to poll bytes over a radio frequency and then outputs each value on the OLED display. Similarly to the [transmitter](#9b) circuit, the receiver circuit communicates with an RF24 module over [SPI](https://en.wikipedia.org/wiki/Serial_Peripheral_Interface).
 
<figure id="9c">
  <img src="./rx2.jpg">
  <p><a>Figure 9C</a>: The receiver circuit. An Arduino Micro which connects to an OLED display over I<sup>2</sup>C and communicates with an RF24 module over SPI.</p>
</figure>

The code for the [transmitter](#9b) can be downloaded <a download href="./Simple_xmit.ino">here</a> or copy-and-pasted from down below:
```cpp
/*
  Simple sketch for nRF24L01+ radios.  Transmit side.
*/

#include <SPI.h>
#include "RF24.h"

/* Hardware configuration: Set up nRF24L01 radio on SPI bus plus pins 7 & 8 */
RF24 radio(7, 8);

byte addresses[][6] = {"1Kosi", "2Kosi"};

void setup() {
  Serial.begin(115200);
  Serial.println(F("RF24example:  Simple tx"));

  radio.begin();

  // Set the PA Level low to prevent power supply related issues since this is a
  // getting_started sketch, and the likelihood of close proximity of the devices. RF24_PA_MAX is default.
  radio.setPALevel(RF24_PA_LOW);

  // Open a writing and reading pipe on each radio, with opposite addresses

  radio.openWritingPipe(addresses[0]);
  radio.openReadingPipe(1, addresses[1]);
}

void loop() {
  Serial.println("Now sending");

  byte data = analogRead(A0) / 4;

  if (!radio.write( &data, 1 )) {
    Serial.println(F("failed"));
  }

  Serial.print("Sent ");
  Serial.println(data);

  // Try again 1s later
  delay(1000);

} // loop end
```

Likewise, the code for the [receiver](#9c) can be downloaded <a download href="./Simple_rec.ino">here</a> or copy-and-pasted from down below:

```cpp
/*
  Simple sketch for nRF24L01+ radios  Receive side.
*/

#include <SPI.h>
#include <Wire.h>
#include "RF24.h"
#include <Adafruit_SSD1306.h>
#include <Adafruit_GFX.h>

#define WIDTH 128
#define HEIGHT 64
#define OLED_RESET -1

/* Hardware configuration: Set up nRF24L01 radio on SPI bus plus pins 7 & 8 */
RF24 radio(7, 8);

Adafruit_SSD1306 display(WIDTH, HEIGHT, &Wire, OLED_RESET);

byte addresses[][6] = {"1Kosi", "2Kosi"};

void print_monitor(int val) {
  display.clearDisplay();

  display.setTextSize(2);
  display.setTextColor(WHITE);
  display.setCursor(0, 0);
  display.println(val);

  display.display();
}

void setup() {

  Serial.begin(115200);
  Serial.println(F("RF24example:  simple receive"));
  //myservo.attach(2);

  if (!display.begin(SSD1306_SWITCHCAPVCC, 0x3C))
  {
    Serial.println(F("SSD1306 allocation failed"));
    for (;;)
    {
    } // stuck forever
  }

  // Clear the display
  display.clearDisplay();

  radio.begin();

  // Set the PA Level low to prevent power supply related issues since this is a
  // getting_started sketch, and the likelihood of close proximity of the devices. RF24_PA_MAX is default.
  radio.setPALevel(RF24_PA_LOW);


  radio.openWritingPipe(addresses[1]);
  radio.openReadingPipe(1, addresses[0]);
  
  // Start the radio listening for data
  radio.startListening();
}

void loop() {

  byte rec_data;

  if ( radio.available()) {
    // Variable for the received timestamp
    while (radio.available()) {
      radio.read( &rec_data, 1 );
    }
    print_monitor(rec_data);
    Serial.print("received ");
    Serial.println(rec_data);
  }
} // Loop
```

### Complications
According to the [pinout](http://tmrh20.github.io/RF24/) (scroll down) attached to Rob's introudction to RF24 modules, certain pins are supposed to be connected others for Arduino Uno's. However, since the Arduino Micro uses an ATMega32u4 (opposed to the ATMega328P found in the Arduino Nano and Uno), the pinout for SPI enabled lines differ. As a result I used the [pinout.guide's pinout diagram for the Arduino Micro](https://pinout.guide/arduino_micro_pinout_and_ISP_pins.png) to connect the correct SPI lines from the RF24 module properly (i.e. MOSI, SCK, and MISO).

The second complication I encountered was with data length. Typically most OSI protocols have a systematic method of communicating with each other known as a [packet](https://en.m.wikipedia.org/wiki/Network_packet) since small amounts of physical data can be transfered across physical layers at a time. However, with the short time available in class I was not able to implement such software. Therefore, I circumvented the issue where the Arduino's `analogRead()` function returns an integer between 0 and 1023 by simply integer dividing the result of the function by 4 (so the maximum is 255 or 0xFF, which is the greatest value of a byte).